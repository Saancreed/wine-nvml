pkgname=wine-nvml
pkgver=0.5
pkgrel=1
pkgdesc='NVIDIA Management Library wrapper for Wine'
arch=('x86_64')
url='https://github.com/Saancreed/wine-nvml'
license=('LGPL-2.1-or-later')
depends=('glibc' 'wine')
makedepends=('git' 'meson' 'mingw-w64-gcc' 'ninja')
options=('pestrip')
source=("git+https://github.com/Saancreed/wine-nvml.git${_fragment:+#${_fragment}}")
sha512sums=('SKIP')

prepare()
{
    cd "${srcdir}/wine-nvml/src" && ./make_nvml
}

pkgver()
{
    git -C "${srcdir}/wine-nvml" describe --tags | sed -E 's/^v//;s/-([0-9]+)-/-r\1-/;s/-/./g'
}

build()
{
    cd "${srcdir}/wine-nvml"

    arch-meson \
        --cross-file ./cross-mingw64.txt \
        ../build-mingw64 .

    ninja -C ../build-mingw64

    arch-meson \
        --cross-file ./cross-wine64.txt \
        ../build-wine64 .

    ninja -C ../build-wine64
}

package()
{
    depends+=('nvidia-utils')

    DESTDIR="${pkgdir}" ninja -C "${srcdir}/build-mingw64" install
    DESTDIR="${pkgdir}" ninja -C "${srcdir}/build-wine64" install

    find "${pkgdir}" -name '*.dll.a' -delete
}
