pkgname=wine-nvml
pkgver=0.4
pkgrel=1
pkgdesc='NVIDIA Management Library wrapper for Wine'
arch=('x86_64')
url='https://github.com/Saancreed/wine-nvml'
license=('LGPL2.1')
makedepends=('git' 'meson' 'mingw-w64-gcc' 'ninja' 'wine')
options=('!buildflags' '!strip')
source=("git+https://github.com/Saancreed/wine-nvml.git${_fragment:+#${_fragment}}")
sha512sums=('SKIP')

prepare()
{
    cd "${srcdir}/wine-nvml/src" && ./make_nvml
}

pkgver()
{
    git -C "${srcdir}/wine-nvml" describe --tags | sed -E 's/^v//;s/-([0-9]+)-/-r\1-/;s/-/./g'
}

build()
{
    cd "${srcdir}/wine-nvml"

    meson setup \
        --cross-file ./cross-mingw64.txt \
        --prefix /usr \
        --libdir lib \
        --buildtype release \
        --strip \
        ../build-mingw64 .

    ninja -C ../build-mingw64

    meson setup \
        --cross-file ./cross-wine64.txt \
        --prefix /usr \
        --libdir lib \
        --buildtype release \
        --strip \
        ../build-wine64 .

    ninja -C ../build-wine64
}

package()
{
    depends=('nvidia-utils' 'wine')

    DESTDIR="${pkgdir}" ninja -C "${srcdir}/build-mingw64" install
    DESTDIR="${pkgdir}" ninja -C "${srcdir}/build-wine64" install

    find "${pkgdir}" -name '*.dll.a' -delete
}
