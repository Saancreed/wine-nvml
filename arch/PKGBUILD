pkgname=wine-nvml
pkgver=0.1
pkgrel=1
pkgdesc='NVIDIA Management Library wrapper for Wine'
arch=('x86_64')
url='https://github.com/Saancreed/wine-nvml'
license=('LGPL2.1')
makedepends=('git' 'meson' 'ninja' 'wine')
source=("git+https://github.com/Saancreed/wine-nvml.git#tag=v${pkgver}"
        'git+https://github.com/NVIDIA/nvidia-settings.git'
        'git+https://github.com/wine-mirror/wine.git')
sha512sums=('SKIP' 'SKIP' 'SKIP')

prepare()
{
    cd "${srcdir}/wine-nvml"

    git submodule init

    for _submodule in nvidia-settings wine
    do
        git config "submodule.submodules/${_submodule}.url" "${srcdir}/${_submodule}"
    done

    git submodule update
}

pkgver()
{
    git -C "${srcdir}/wine-nvml" describe --tags | sed -E 's/^v//;s/-([0-9]+)-/-r\1-/;s/-/./g'
}

build()
{
    cd "${srcdir}/wine-nvml"

    meson setup ../build64 \
        --cross-file ./build-wine64.txt \
        --prefix /usr \
        --libdir lib \
        --buildtype release \
        --strip

    ninja -C ../build64

    meson setup ../build32 \
        --cross-file ./build-wine32.txt \
        --prefix /usr \
        --libdir lib32 \
        --buildtype release \
        --strip

    ninja -C ../build32
}

package()
{
    depends=('lib32-nvidia-utils' 'nvidia-utils' 'wine')

    DESTDIR="${pkgdir}" ninja -C "${srcdir}/build64" install
    DESTDIR="${pkgdir}" ninja -C "${srcdir}/build32" install
}
